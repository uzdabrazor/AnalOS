diff --git a/chrome/browser/analos/server/BUILD.gn b/chrome/browser/analos/server/BUILD.gn
new file mode 100644
index 0000000000000..711d6896d6c4a
--- /dev/null
+++ b/chrome/browser/analos/server/BUILD.gn
@@ -0,0 +1,77 @@
+# Copyright 2024 The Chromium Authors
+# Use of this source code is governed by a BSD-style license that can be
+# found in the LICENSE file.
+
+import("//build/config/chrome_build.gni")
+
+# Validate that required resources exist at build time
+# GN will fail at generation time if resources/bin/analos_server is missing
+_analos_binary_name = "analos_server"
+if (is_win) {
+  _analos_binary_name += ".exe"
+}
+
+action("validate_analos_resources") {
+  script = "validate_resources.py"
+  inputs = [ "resources/bin/${_analos_binary_name}" ]
+  outputs = [ "$target_gen_dir/analos_resources_validated" ]
+}
+
+source_set("server") {
+  sources = [
+    "analos_appcast_parser.cc",
+    "analos_appcast_parser.h",
+    "analos_server_constants.h",
+    "analos_server_manager.cc",
+    "analos_server_manager.h",
+    "analos_server_prefs.cc",
+    "analos_server_prefs.h",
+    "analos_server_updater.cc",
+    "analos_server_updater.h",
+  ]
+
+  deps = [
+    "//base",
+    "//chrome/browser:browser_process",
+    "//chrome/browser/analos/metrics",
+    "//chrome/common",
+    "//components/prefs",
+    "//content/public/browser",
+    "//crypto",
+    "//net",
+    "//services/network/public/cpp",
+    "//third_party/boringssl",
+    "//third_party/libxml:xml_reader",
+    "//third_party/zlib/google:zip",
+  ]
+}
+
+if (is_mac) {
+  import("//build/config/apple/symbols.gni")
+  import("//build/config/mac/mac_sdk.gni")
+
+  # Bundle data for macOS - recursively packages resources/ to Resources/AnalOSServer/default/
+  bundle_data("analos_resources_bundle") {
+    sources = [ "resources" ]
+    outputs = [ "{{bundle_resources_dir}}/AnalOSServer/default/{{source_file_part}}" ]
+    # TODO: Re-enable validation when resources/bin/analos_server is available
+    # deps = [ ":validate_analos_resources" ]
+  }
+} else {
+  # Copy for Windows/Linux - recursively packages resources/ to <exe_dir>/AnalOSServer/default/
+  copy("analos_resources_copy") {
+    sources = [ "resources" ]
+    outputs = [ "$root_out_dir/AnalOSServer/default/{{source_file_part}}" ]
+    # TODO: Re-enable validation when resources/bin/analos_server is available
+    # deps = [ ":validate_analos_resources" ]
+  }
+}
+
+# Group for all AnalOS server resources
+group("analos_server_resources") {
+  if (is_mac) {
+    deps = [ ":analos_resources_bundle" ]
+  } else {
+    deps = [ ":analos_resources_copy" ]
+  }
+}
